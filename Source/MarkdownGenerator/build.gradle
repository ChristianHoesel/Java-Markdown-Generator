plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'

group 'net.steppschuh.markdowngenerator'
version = '1.0'

repositories {
    mavenCentral()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'generic'
        name = 'Markdown Generator'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Steppschuh/Java-Markdown-Generator.git'
        version {
            name = '1.0'
            desc = 'Markdown Generator 1.0'
            released = new Date()
            vcsTag = '1.0.0'
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Steppschuh"
            name "Stephan Schultz"
            email "contact@steppschuh.net"
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'net.steppschuh.markdowngenerator'
            artifactId 'markdowngenerator'
            version '1.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java library to generate markdown')
                root.appendNode('name', 'Markdown Generator')
                root.appendNode('url', 'https://github.com/Steppschuh/Java-Markdown-Generator')
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}